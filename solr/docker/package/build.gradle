/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'Solr Docker Package image'

apply plugin: 'base'

configurations {
  dockerPackage {
    canBeConsumed = true
    canBeResolved = true
  }
}

// The solr package docker image relies on the output of the solr:packaging project.
Project solrPackaging = project(':solr:packaging')

task dockerTar(type: Tar, dependsOn: solrPackaging.tasks.distTar) {
  group = 'Docker'
  description = 'Package docker context to prepare for solr-package docker build'

  into('releases') {
    from solrPackaging.tasks.distTar.outputs
  }

  from file('Dockerfile.local-package')
  rename 'Dockerfile.*', 'Dockerfile'
  destinationDirectory = file('build/distributions')
  extension 'tgz'
  compression = Compression.GZIP
}

task dockerBuild(type: Exec, dependsOn: tasks.dockerTar) {
  group = 'Docker'
  description = 'Build Solr package docker image'

  standardInput = tasks.dockerTar.outputs.files.singleFile.newDataInputStream()
  commandLine "docker", "build", "-t", "apache/solr-build:${version}", "--iidfile", "$buildDir/image-id", "-"

  outputs.files("$buildDir/image-id")
}

artifacts {
  dockerPackage(tasks.dockerBuild.outputs.files.singleFile) {
    builtBy(tasks.dockerBuild)
  }
}