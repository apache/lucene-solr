/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.lucene.gradle.CheckWorkingCopy
import org.apache.lucene.gradle.LuceneSolrForbiddenApisPlugin

plugins {
  id "com.palantir.consistent-versions" version "1.8.0"
}

buildDir = new File("build")

// define lucene-solr project lists that exclude buildSrc
def luceneSolrProjects = allprojects.findAll { project -> project.name != 'buildSrc'};
def luceneSolrSubProjects = subprojects.findAll { project -> project.name != 'buildSrc'}

def rootProjectDir = project.rootProject.projectDir;

ant.lifecycleLogLevel = "INFO"

// TOC
// -> lucene-solr all project config
// -> lucene-solr sub module config
// -> lucene-solr root project config
// -> lucene-solr IDE config
// -> other config

// -> lucene-solr all module config - configure all lucene-solr projects, including root project
configure(luceneSolrProjects) {

  // setup repositories
  apply from: new File(rootProjectDir, "buildSrc/common/configure-repositories.gradle")
  
  buildDir = new File("build")
  
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: 'maven-publish'
  
  task ratSources(type: org.apache.lucene.gradle.RatSources) {
    group = 'Verification'
    description = "Checks source files for proper license headers."
    excludes = ['**/TODO', '**/*.txt,', '**/*.iml']
    check.dependsOn ratSources
  }
  
  task checkSourcePatterns(type: org.apache.lucene.gradle.CheckSourcePatterns) {
    group = 'Verification'
    description = "Checks source files for project violations."
    baseDir = projectDir
    check.dependsOn checkSourcePatterns
  }
  
  task checkMissingJavaDocs {
    group = 'Verification'
    description = "Checks for missing JavaDocs."
    
    doLast {
      project.exec {
        workingDir project.projectDir.getAbsolutePath()
        executable "${python_exe}"
        args = ['-B', rootProjectDir.getAbsolutePath() + '/dev-tools/scripts/checkJavaDocs.py', project.projectDir.getAbsolutePath(), 'class']
      }
    }
  }
  
  // TODO: check.dependsOn checkMissingJavaDocs
  
  task listDeps(type: org.apache.lucene.gradle.ListDeps) {
    group = 'Help'
    description = "List deps for a module."
  }
}

// -> lucene-solr sub module config - configure all lucene-solr modules (excludes the root project)
configure(luceneSolrSubProjects) {
  
  plugins.withType(JavaPlugin) {

    project.plugins.apply(LuceneSolrForbiddenApisPlugin)
    
    sourceCompatibility = "11"
    targetCompatibility = "11"
    
    // Use UTF-8, don't rely on local platform encoding.
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"
    
    sourceSets {
      main.java.srcDirs = ['src/java']
      main.resources.srcDirs = ['src/resources']
      test.java.srcDirs = ['src/test']
      test.resources.srcDirs = ['src/test-files']
    }
    
    // configure tests
    project.apply from: new File(rootProjectDir, "buildSrc/common/configure-test.gradle")
    
    // Custom javac options
    tasks.withType(JavaCompile) {
      // Don't output ALL errors in a file, stop after a few
      options.compilerArgs << "-Xmaxerrs" << "5"
    }

    task sourceJar(type: Jar) {
      classifier 'sources'
      from sourceSets.main.allJava
    }
    
    // configure maven
    publishing {
      publications {
        mavenJava(MavenPublication) {
          from components.java
          artifact tasks.sourceJar
        }
      }
    }
  }
}

// -> lucene-solr root project config - configure the root project
configure(rootProject) {
  
  // root project config
  
  // TODO: enable the modified files checking only for jenkins runs! sysprop?
  tasks.create('checkWorkingCopy', CheckWorkingCopy, false)
  
  
  task testTimes(type: org.apache.lucene.gradle.JUnit4) {
    group = 'Tests'
    description = "Show the slowest tests (averages)."
  }
  
  task pristineClean(type: org.apache.lucene.gradle.PristineClean) {}
  
}

// -> lucene-solr IDE config - setup eclipse and idea

configure(allprojects) {
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  
  plugins.withType(JavaPlugin) {
    project.apply from: new File(rootProjectDir, "buildSrc/ide/eclipse.gradle")
    project.apply from: new File(rootProjectDir, "buildSrc/ide/idea.gradle")
  }
}

// -> other config

configure(allprojects) {

  dependencies {
    modules {
      module("commons-logging:commons-logging") { replacedBy("org.slf4j:jcl-over-slf4j") }
    }
  }
}

// Single JavaDocs for all modules

def noJavaDocModules = ["buildSrc", "dev-tools","lucene", "solr", "solr-ref-guide", "analysis", "backward-codecs", "contrib", "example"]
def javaDocProjects = subprojects.findAll { project -> !noJavaDocModules.contains(project.name)}

project.afterEvaluate{
  task javadocAll(type: Javadoc) {
    group = 'Documentation'
    description = "Generates single JavaDoc for all modules."
    javaDocProjects.each { evaluationDependsOn(it.path) }
    source javaDocProjects.collect { it.sourceSets.main.allJava }
    classpath = files(javaDocProjects.collect { it.sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs")
  }
}

