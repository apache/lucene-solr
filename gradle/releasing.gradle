/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.commons.codec.digest.DigestUtils

// We're using commons-codec for computing checksums.
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "commons-codec:commons-codec:${scriptDepVersions['commons-codec']}"
    }
}

allprojects {
    plugins.withType(DistributionPlugin) {
        def checksum = {
            outputs.files.each { File file ->
                String sha512 = new DigestUtils(DigestUtils.sha512Digest).digestAsHex(file).trim()
                new File(file.parent, file.name + ".sha512").write(sha512 + "  " + file.name, "UTF-8")
            }
        }

        distZip {
            doLast checksum
        }

        distTar {
            compression = Compression.GZIP
            doLast checksum
        }

        installDist {
            doLast {
                logger.lifecycle "Distribution assembled under: ${destinationDir}"
            }
        }
    }
}
