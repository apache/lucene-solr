/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// This adds top-level 'precommit' task.

configure(rootProject) {
  task precommit() {
    group = 'Precommit'
    description = "All precommit checks"

    // Root-level validation tasks.
    dependsOn ":verifyLocks"
    dependsOn ":versionsPropsAreSorted"
    dependsOn ":checkWorkingCopyClean"

    // Solr validation tasks.
    dependsOn ":solr:validateConfigFileSanity"

    // Attach all these tasks from all projects that have them.
    // This uses lazy collections as they may not yet be defined.
    dependsOn allprojects.collect { prj ->
      prj.tasks.matching { task -> task.name in [
          "forbiddenApis",
          "licenses",
          "javadoc",
          "rat",
          "ecjLint",
          "validateLogCalls",
          "validateSourcePatterns",
          "spotlessCheck"
      ]}
    }
  }

  // Each validation task should be attached to check but make sure
  // precommit() as a whole is a dependency on rootProject.check
  check.dependsOn precommit
}

