
def resources = scriptResources(buildscript)
 
allprojects { prj ->
  plugins.withType(JavaPlugin) {
    prj.apply plugin: 'com.diffplug.spotless'

    spotless {
      java {
        licenseHeaderFile file("${resources}/asl-header.txt"), '^(\\s*package)'
        lineEndings 'UNIX'
        endWithNewline()
        googleJavaFormat('1.9')

        // Known problematic files.
        targetExclude "**/HTMLStripCharFilter.java", "**/UAX29URLEmailTokenizerImpl.java", 
           "**/PatternParser.java", "**/BuildNavDataFiles.java", "**/CheckLinksAndAnchors.java",
           "**/TestSubQueryTransformer.java"
      }

      check.dependsOn(spotlessCheck)
    }

    task tidy() {
      description "Applies automatic format to Java sources."
      group "Verification"

      dependsOn spotlessApply
    }
  }
}
