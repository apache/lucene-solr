import java.nio.charset.StandardCharsets

/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// This ensures 'gradlew*' scripts contain the manual fix so that they
// don't fork a daemon subprocess on the initial run.

configure(rootProject) {
  task gradlewScriptsTweaked() {
    def scripts = [
        file("gradlew"),
        file("gradlew.bat")
    ]

    inputs.files(scripts)
    outputs.upToDateWhen { true }

    doFirst {
      scripts.each { file ->
        def content = new String(file.readBytes(), StandardCharsets.US_ASCII)
        if (content.indexOf("Don't fork a daemon mode on initial run that generates local defaults") < 0) {
          throw new GradleException("Launch script ${file} does not have a manual daemon tweak (see LUCENE-9232).")
        }
      }
    }
  }

  check.dependsOn gradlewScriptsTweaked
}
