/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// This adds a configuration and artifact to solr-core which exports "post.jar" tool.
// this should be a separate project instead (it is self-contained and classes are reused
// in many places).

configure(project(":solr:core")) {
  plugins.withType(JavaPlugin) {
    configurations {
      postJar
    }

    task assemblePostJar(type: Jar) {
      dependsOn classes

      archiveFileName = "post.jar"
      destinationDirectory = file("${buildDir}/postJar")

      from(sourceSets.main.output, {
        include "org/apache/solr/util/CLIO.class"
        include "org/apache/solr/util/SimplePostTool*.class"
        include "org/apache/solr/util/RTimer*.class"
      })

      manifest {
        attributes("Main-Class": "org.apache.solr.util.SimplePostTool")
      }
    }

    artifacts {
      postJar assemblePostJar
    }

    assemble.dependsOn assemblePostJar
  }
}