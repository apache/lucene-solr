/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: "de.undercouch.download"

configure(rootProject) {
  configurations {
    utilgen
  }

  dependencies {
  }

  task utilgen {
    description "Regenerate sources for ...lucene/util/automaton and ...lucene/util/packed."
    group "generation"

    dependsOn ":lucene:core:utilGenPacked"
    dependsOn ":lucene:core:utilGenLev"
  }
}

task installMoman(type: Download) {
  def momanDir = new File(buildDir, "moman").getAbsolutePath()
  def momanZip = new File(momanDir, "moman.zip").getAbsolutePath()

  src "https://bitbucket.org/jpbarrette/moman/get/5c5c2a1e4dea.zip"
  dest momanZip
  onlyIfModified true

  doLast {
    logger.lifecycle("Downloading moman to: ${buildDir}")
    ant.unzip(src: momanZip, dest: momanDir, overwrite: "true") {
      ant.cutdirsmapper(dirs: "1")
    }
  }
}

configure(project(":lucene:core")) {
  task utilGenPacked(dependsOn: installMoman) {
    description "Regenerate util/PackedBulkOperationsPacked*.java and Packed64SingleBlock.java"
    group "generation"

    def workDir = "src/java/org/apache/lucene/util/packed"

    doLast {
      ['gen_BulkOperation.py', 'gen_Packed64SingleBlock.py'].each { prog ->
        logger.lifecycle("Executing: ${prog} in ${workDir}")
        project.exec {
          workingDir workDir
          executable "python"
          args = ['-B', "${prog}"]
        }
      }
      // Correct line endings for Windows.
      project.ant.fixcrlf(
          srcDir: workDir,
          includes: 'Packed64SingleBlock.java, BulkOperation*.java',
          encoding: 'UTF-8',
          eol: 'lf'
      )
    }
  }

  task utilGenLev(dependsOn: installMoman) {
    description "Regenerate util/automaton Lev*ParametricDescription.java"
    group "generation"

    doLast {
      def workDir = 'src/java/org/apache/lucene/util/automaton'

      ['1', '2'].each { num ->
        ['True', 'False'].each { transpose ->
          project.exec {
            workingDir workDir
            executable "python"
            args = ['-B', 'createLevAutomata.py', num, transpose]
          }
        }
      }
      project.ant.fixcrlf(
          srcDir: workDir,
          includes: '*ParametricDescription.java',
          encoding: 'UTF-8',
          eol: 'lf'
      )
    }
  }
}