/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */ 

// See LUCENE-9471. We redirect temporary location for gradle
// so that it doesn't pollute user's tmp. Wipe it during a clean though.

configure(rootProject) {
  task cleanGradleTmp(type: Delete) {
    delete fileTree(".gradle/tmp").matching {
      include "gradle-worker-classpath*"
    }
  }

  clean.dependsOn cleanGradleTmp
}

// Make sure we clean up after running tests.
allprojects {
  plugins.withType(JavaPlugin) {
    def temps = []

    task cleanTaskTmp() {
      doLast {
        temps.each { temp ->
          project.delete fileTree(temp).matching {
            include "jar_extract*"
          }
        }
      }
    }

    tasks.withType(Test) {
      finalizedBy rootProject.cleanGradleTmp, cleanTaskTmp
      temps += temporaryDir
    }
  }
}
