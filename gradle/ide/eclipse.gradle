/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

configure(rootProject) {
  apply plugin: "eclipse"

  def projects = [":lucene:core"]

  eclipse {
    project {
      name = "Apache Lucene Solr ${version}"
    }

    classpath {
      defaultOutputDir = file('build/eclipse')
      
        file {
          beforeMerged { classpath -> classpath.entries.removeAll { it.kind == "src" } }

          whenMerged { classpath ->
            def sources = []
            projects.each { name ->
               project(name).sourceSets.main.java.srcDirs.findAll { dir -> dir.exists() }.each { dir ->
                 sources += new org.gradle.plugins.ide.eclipse.model.SourceFolder(
                   dir.toString(), "build/eclipse"
                 )
               }
            }

            classpath.entries += sources
          }
      }
    }

    jdt {
      sourceCompatibility = "11"
      targetCompatibility = "11"
      javaRuntimeName = "JavaSE-11"
    }
  }
}
