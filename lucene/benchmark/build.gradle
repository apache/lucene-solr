/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: org.apache.lucene.gradle.dist.PartOfDist

def workingDir = file("work")

dependencies {
  
  implementation project(':lucene:lucene-core')
  implementation project(':lucene:lucene-analyzers:lucene-analyzers-common')
  implementation project(':lucene:lucene-facet')
  implementation project(':lucene:lucene-highlighter')
  implementation project(':lucene:lucene-queries')
  implementation project(':lucene:lucene-spatial-extras')
  implementation project(':lucene:lucene-queryparser')
  
  implementation ("org.apache.commons:commons-compress")
  implementation ("xerces:xercesImpl")
  implementation ("net.sourceforge.nekohtml:nekohtml")
  implementation ("com.ibm.icu:icu4j")
  implementation ("org.locationtech.spatial4j:spatial4j")
  
  testImplementation project(':lucene:lucene-test-framework')
}

// copy .alg files as resources for testing
task copyConfResources(type: Copy) {
  from('conf')
  into mfile(sourceSets.test.java.outputDir, "conf")
}
processTestResources.dependsOn copyConfResources

// Enwiki

task downloadEnwiki(type: org.apache.lucene.gradle.Download) {
  sourceUrl = "http://home.apache.org/~dsmiley/data/enwiki-20070527-pages-articles.xml.bz2"
  target = file("temp/enwiki-20070527-pages-articles.xml.bz2")
}

task installEnwiki(){
  group = 'Benchmark Data'
  description = "Installs Enwiki data files."
  doLast {
    ant.bunzip2(src: "${projectDir}/temp/enwiki-20070527-pages-articles.xml.bz2", dest:"${projectDir}/temp/")
  }
}

installEnwiki.dependsOn downloadEnwiki

// GeoNames

task downloadGeoNames(type: org.apache.lucene.gradle.Download) {
  sourceUrl = "http://home.apache.org/~dsmiley/data/geonames_20130921_randomOrder_allCountries.txt.bz2"
  target = mfile('temp', 'allCountries.txt.bz2')
}

task installGeoNames() {
  group = 'Benchmark Data'
  description = "Installs GeoNames data files."
  doLast {
    mkdir("${workingDir}/geonames")
    ant.bunzip2(src: "${projectDir}/temp/allCountries.txt.bz2", dest:"${workingDir}/geonames")
  }
  dependsOn downloadGeoNames
}

// Reuters

task downloadReuters(type: org.apache.lucene.gradle.Download) {
  sourceUrl = "http://www.daviddlewis.com/resources/testcollections/reuters21578/reuters21578.tar.gz"
  target = file("${projectDir}/temp/reuters21578.tar.gz")
}

task extractReuters(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = 'org.apache.lucene.benchmark.utils.ExtractReuters'
  args filePath("${workingDir}/reuters")
  args filePath("${workingDir}/reuters-out")
  dependsOn downloadReuters
}

task installReuters(){
  group = 'Benchmark Data'
  description = "Installs Reuters data files."
  doLast {
    mkdir("${workingDir}/reuters")
    ant.gunzip(src: filePath("${projectDir}/temp/reuters21578.tar.gz"), dest:"${projectDir}/temp/")
    ant.untar(src: filePath("${projectDir}/temp/reuters21578.tar"), dest: "${workingDir}/reuters")
    def tree = fileTree("${workingDir}/reuters")
    tree.include '**/*.txt'
    tree.each { it.delete() }
  }
  dependsOn extractReuters
}

forbiddenApisMain {
  bundledSignatures -= 'jdk-system-out'
}

missingDeps {
  classExclude 'org\\.noggit\\.JSONParser' // we don't currently pull in noggit
  
  foundInClassExclude  'org\\.apache\\.commons\\.compress\\.compressors\\..*' // there are optional compressor deps we don't have
}

