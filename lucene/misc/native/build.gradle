/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 * This gets separated out from misc module into a native module due to incompatibility between cpp-library and java-library plugins.
 * For details, please see https://github.com/gradle/gradle-native/issues/352#issuecomment-461724948
 */
import org.apache.tools.ant.taskdefs.condition.Os

description = 'Module for native code'

apply plugin: 'cpp-library'

library {
  baseName = 'LuceneNativeIO'

  // Native build for Windows platform will be added in later stage
  targetMachines = [
      machines.windows.x86_64
  ]

  // Point at platform-specific sources. Other platforms will be ignored
  // (plugin won't find the toolchain).
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    source.from file("${projectDir}/src/main/windows")
  }
}

tasks.withType(CppCompile).configureEach {
  def javaHome = rootProject.ext.runtimeJava.getInstallationDirectory().getAsFile().getPath()

  // Assume standard openjdk layout. This means only one architecture-specific include folder
  // is present.
  systemIncludes.from file("${javaHome}/include")

  for (def path : [file("${javaHome}/include/win32")]) {
    if (path.exists()) {
      systemIncludes.from path
    }
  }

  compilerArgs.add '-fPIC'
}

tasks.withType(LinkSharedLibrary).configureEach {
  linkerArgs.add '-lstdc++'
}
