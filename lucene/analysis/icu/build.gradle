/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'

archivesBaseName = 'analyzers-icu'

configurations {
  tools
}

dependencies {
  
  api project(':lucene:lucene-core')
  api project(':lucene:analysis:analyzers-common')
  
  api ('com.ibm.icu:icu4j') { transitive = false }
  
  testImplementation project(':lucene:lucene-test-framework')
}

sourceSets {
  tools {
    compileClasspath += sourceSets.main.runtimeClasspath += configurations.tools
    runtimeClasspath += sourceSets.main.runtimeClasspath
  }
  tools.java.srcDirs = ['src/tools/java']
}

task genUtr30DataFiles(type: JavaExec) {
  group = 'Build Regenerate'
  description = "Compiles UTR30 rules."
  
  classpath = sourceSets.tools.runtimeClasspath
  main = 'org.apache.lucene.analysis.icu.GenerateUTR30DataFiles'
  
  workingDir "src/data/utr30"
}

def resourcesDir = "${projectDir}/src/resources"

task gennorm2() {
  group = 'Build Regenerate'
  description = "Compiles norm2 rules. Note that the gennorm2 and icupkg tools must be on your PATH. These tools are part of the ICU4C package. See http://site.icu-project.org/ or sudo apt install icu-devtools"
  doLast {
    new File(buildDir, "gennorm2").mkdirs()
    exec {
      executable "gennorm2"
      args = ['-v', '-s', "${projectDir}/src/data/utr30", 'nfc.txt', 'nfkc.txt', 'nfkc_cf.txt', 'BasicFoldings.txt', 'DiacriticFolding.txt', 'DingbatFolding.txt', 'HanRadicalFolding.txt', 'NativeDigitFolding.txt', '-o', "${buildDir}/gennorm2/utr30.tmp"]
    }
    exec {
      executable "icupkg"
      args = ['-tb', "${buildDir}/gennorm2/utr30.tmp", "${resourcesDir}/org/apache/lucene/analysis/icu/utr30.nrm"]
    }
    delete("${buildDir}/gennorm2/utr30.tmp")
  }
  mustRunAfter genUtr30DataFiles
}

task genrbbi(type: JavaExec) {
  group = 'Build Regenerate'
  description = "Compiles RBBI rules."
  
  classpath = sourceSets.tools.runtimeClasspath
  main = 'org.apache.lucene.analysis.icu.RBBIRuleCompiler'

  enableAssertions = true
  
  args "src/data/uax29"
  args "${projectDir}/src/resources/org/apache/lucene/analysis/icu/segmentation"
}

task regenerate {
  dependsOn genUtr30DataFiles, gennorm2, genrbbi
}

forbiddenApisTools {
  bundledSignatures -= 'jdk-system-out'
  exclude 'org/apache/lucene/**' // exclude until converted to NIO2
}

