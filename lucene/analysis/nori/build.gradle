/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'
apply plugin: org.apache.lucene.gradle.PartOfDist

archivesBaseName = 'analyzers-nori'

configurations {
  tools
}

dependencies {
  implementation project(':lucene:lucene-core')
  implementation project(':lucene:analysis:lucene-analyzers-common')
  
  tools ('com.ibm.icu:icu4j') { transitive = false }
  
  testImplementation project(':lucene:lucene-test-framework')
}

sourceSets {
  tools {
    compileClasspath += sourceSets.main.runtimeClasspath += configurations.tools
    runtimeClasspath += sourceSets.main.runtimeClasspath
  }
  toolsTest {
    compileClasspath += sourceSets.test.runtimeClasspath += sourceSets.tools.runtimeClasspath += configurations.tools
    runtimeClasspath += sourceSets.test.runtimeClasspath
  }

  tools.java.srcDirs = ['src/tools/java']
  toolsTest.java.srcDirs = ['src/tools/test']
}

def mecabVersion = "2.0.3-20170922"
def mecabFilename = "mecab-ko-dic-${mecabVersion}"

task downloadDict(type: org.apache.lucene.gradle.Download) {
  sourceUrl = "https://bitbucket.org/eunjeon/mecab-ko-dic/downloads/${mecabFilename}.tar.gz"
  target = new File("${buildDir}/${mecabFilename}.tar.gz")
  
  doLast {
    ant.gunzip(src: "${buildDir}/${mecabFilename}.tar.gz")
    ant.untar(src: "${buildDir}/${mecabFilename}.tar", dest: "${buildDir}")
  }
}

task buildNoriDict(type: JavaExec) {
  group = 'Build Regenerate'
  description = "Downloads, patches, and builds dict for nori."
  
  doFirst {
    def tree = fileTree("${projectDir}/org/apache/lucene/analysis/ko/dict")
    tree.include '**/*'
    tree.each { it.delete() }
  }
  
  classpath = sourceSets.tools.runtimeClasspath
  main = 'org.apache.lucene.analysis.ko.util.DictionaryBuilder'
  
  enableAssertions = false // TODO: an assertion fails if set to true!
 
  jvmArgs '-Xmx1G'

  args "${buildDir}/${mecabFilename}"
  args "${projectDir}/src/resources"
  args 'utf-8'
  args 'false'
  
  dependsOn downloadDict
}

task regenerate {
  dependsOn buildNoriDict
}

forbiddenApisTools {
  bundledSignatures -= 'jdk-system-out'
  exclude 'org/apache/lucene/**' // exclude until converted to NIO2
}


